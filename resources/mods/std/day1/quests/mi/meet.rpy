init python:
	joined_mus_club = False


label clubs__mus_club:
	if rpg_event != 'no_exit':
		return
	if 'mus_club_meet' in was:
		return
	$ was.append('mus_club_meet')
	
	$ set_rpg_control(False)
	$ mi.set_auto(False)
	
	"Я несмело постучался."
	me "Ау? Есть кто?"
	"Тишина."
	th "Ну, видно не судьба..."
	"..."
	"Нет, так не пойдёт. Раз пришёл, надо попробовать и зайти."
	
	$ me.set_direction(to_forward)
	$ set_location('mus_club', 'clubs')
	
	"Перед моими глазами предстало не очень большое помещение."
	me "Итак, это музклуб... или что-то вроде того."
	
	$ me.move_to_place('before_piano')
	$ me.set_direction(to_left)
	
	"Возле левой стены стояли инструменты, слепя отблесками лучей света."
	th "Так вот как выглядят ещё свежие советские гитары."
	"Будто в замедлившемся времени, минуту я рассматривал стандартные, казалось бы, изгибы... однако было в них что-то завораживающее."
	"Внезапно мне в голову пришла неожиданная мысль."
	th "Почему бы не сыграть? Да, я забросил это дело, толком не раскрутившись... но тут прямо руки чешутся."
	
	$ me.move_to_place('near_instruments')
	$ me.set_direction(to_left)
	"Я аккуратно взял в руки гитару, стоящую за барабанами."
	
	"Я провёл пальцами по струнам, после чего попробовал зажать несколько аккордов (благо, хоть что-то я помнил)."
	"После некоторого времени разминки я начал играть самое простое, что первое пришло в голову."
	
	# играет "Звезда по имени Солнце"
	# $ guitar_hero.start(...)
	
	"..."
	"Доиграв, я положил гитару."
	"Пальцы цепляли то нужные, то ненужные струны, ритм то замедлялся, то ускорялся, аккорды постоянно звучали по-разному."
	th "Нда. Некоторые, наверное, лучше сыграют, даже если в первый раз сядут."
	
	$ show_character(mi, 'clubs')
	$ mi.set_direction(to_forward)
	$ me.set_direction(to_back)
	mi "Ого..."
	"Я встрепенулся."
	
	"Из дверей вышла девочка."
	th "Она мне ужасно напоминает кого-то... вот только кого..."
	th "Стоп. Я же зашёл сюда без приглашения, ещё и взял гитару. Вот блин."
	mi "А ты... Это же ты сегодня приехать должен был?"
	th "Она даже не обратила на это внимания. Впрочем, мне же лучше."
	me "Ну... наверное."
	th "Неужели приезд ничем не примечательного разгильдяя - это целое событие?"
	"Девочка с интересом оглядела меня."
	
	$ mi.move_to_place('before_piano', wait_time = 0.5)
	$ me.set_direction(to_right)
	pause 1
	$ mi.move_to_end()
	$ mi.set_direction(to_left)
	mi "На гитаре играешь?"
	
	me "Сложно это игрой назвать... просто баловался, кхм."
	mi "Нельзя, наверное, прямо с порога, но если хочешь, можешь вступить к нам в клуб! Хотя \"нам\" не совсем правильно говорить, я тут вроде как одна... Но если ты вступишь, нас будет двое!"
	me "Эм... Ну..."
	mi "А, забыла представиться. Меня Мику зовут, а тебя?"
	$ meet('mi', 'Мику')
	
	$ me.set_direction(to_back)
	th "Мику! Мику, Мику, Мику... А-ай, прямо вертится в голове, но не могу вспомнить! Точно же должен её знать!"
	mi "Эй..."
	$ me.set_direction(to_right)
	me "А? А, да. Я Семён."
	mi "Прият..."
	"Видимо, мой озадаченный взгляд вынудил её на объяснения."
	mi "Ой, ты, наверное, думаешь, почему моё имя такое странное? Э-э... Вообще это из-за родни из-за границы."
	mi "В общем, потом объясню. Так ты как... хочешь вступить?"
	
	menu:
		"Я подумаю (уйти)":
			me "Эм... Как-то резко это..."
			mi "Я не буду надоедать! Честно-честно! Сможешь приходить, когда хочешь."
			me "Ну... я подумаю."
			"Мику слегка погрустнела."
			mi "Ладно... можешь прийти ещё после обеда."
			$ mi.set_direction(to_back)
			mi "Если захочешь, конечно..."
			me "А, спасибо. До скорого... наверное."
			$ mi.get_actions().start('home')
			mi "Пока."
		"Я вступаю":
			call mus_club_meet_joining
			$ mi.get_actions().start('home')
	
	$ me.allow_exit('clubs', 'mus_club')
	$ set_rpg_control(True)
	window hide


label mus_club_meet_joining:
	$ joined_mus_club = True
	me "А график какой?"
	mi "Свободный. Можешь приходить всегда, когда я буду здесь."
	th "Хм. Не знаю, из себя я ничего ценного не представляю... Но у неё такой жалостный взгляд."
	me "Ну... только если сможешь терпеть мои классные навыки."
	"Глаза Мику загорелись."
	$ mi.x -= 10
	mi "Конечно смогу! Этот кружок ведь и создан для того, чтобы учиться играть!"
	$ me.x -= 5
	me "Тогда я согласен. Наверное."
	mi "Правда? Спасибо-спасибо-спасибо!"
	"Мику даже немного подпрыгивала от радости, причина которой, естественно, была мне непонятна."
	mi "Уф... Я думаю, что тогда не стоит терять времени..."
	me "Эм... Предлагаешь начать занятие прямо сейчас?"
	mi "Ну да. Ты же не против?"
	th "Опять эти глаза."
	th "Ну как тут можно отказать..."
	
	# $ guitar_hero.start(...)
	
	"..."
	th "Чёрт, как же болят пальцы..."
	mi "Ну, на сейчас хватит. А то если продолжим, может, до конца смены больше не сыграешь."
	me "Хм. Спасибо."
	mi "Не за что! Если хочешь, приходи ещё после обеда. И после ужина тоже можно."
	me "Хорошо. Эм... До встречи?"
	mi "До встречи!"
	$ mi.rp += 1
